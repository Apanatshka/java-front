module languages/Java-1.5/classes/trans/desugar

imports
	
  include/Java
  names

rules // Default access modifier
	
	desugar-add-default-modifier:
		m* -> [Default()|m*]
		where
			<not(fetch(?Public() <+ ?Protected() <+ ?Private()))> m*
			
	desugar = ClassDecHead(desugar-add-default-modifier, id, id, id, id)
	desugar = ConstrDecHead(desugar-add-default-modifier, id, id, id, id)
	desugar = MethodDecHead(desugar-add-default-modifier, id, id, id, id, id)
	desugar = DeprMethodDecHead(desugar-add-default-modifier, id, id, id, id, id, id)

rules // Multiple implements declarations
  
  desugar: 
    ImplementsDec(interface*) -> <map(desugar-implementsdec)> interface*
    
  desugar-implementsdec:
    interface -> ImplementsDec(interface)
    
rules // Multiple field declarations

  desugar:
    FieldDec(m*, t, vardec*) -> <map(desugar-fielddec(|<desugar-add-default-modifier> m*, t))> vardec*
    
  desugar-fielddec(|m*, t):
    v -> FieldDec(m*, t, v)
