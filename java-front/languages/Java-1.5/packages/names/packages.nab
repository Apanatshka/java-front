module languages/Java-1.5/packages/names/packages

imports

	languages/Java-1.5/classes/names/classes
	languages/Java-1.5/types/names/types
  include/Java

namespaces

  Package
  
properties

	pkgannotations of Package : List(Anno)

binding rules // Package

	// TODO: the AST needs to be desugared to have nested package declarations like this.
  PackageDec(a*, p):
    defines non-unique Package p
    	of pkgannotations a*
    scopes Package, Type

binding rules // Imports

  TypeImportDec(i):
    imports 
    	Type i 
    	into subsequent scope
    
  TypeImportOnDemandDec(i):
    imports 
    	Type 
    	from Package i 
    	into subsequent scope
    otherwise
    	imports 
	    	Type 
	    	from Type i 
	    	into subsequent scope
  
  // TODO: modifiers is a list.
  StaticImportDec(t, s):
    imports 
    	Method s 
    		of modifiers Static() 
  		from Type t 
  		into subsequent scope
    otherwise
    	imports
    		Field s 
    			of modifiers Static() 
  			from Type t
  			into subsequent scope
  
  // TODO: modifiers is a list.
  StaticImportOnDemandDec(t):
    imports 
      Method, Field 
        of modifiers Static()
      from Type t 
      into subsequent scope
