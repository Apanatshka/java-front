module languages/Java-1.5/expressions/types/invoke

imports
	
	include/Java
	lib/nabl/-
	lib/task/-
	lib/types/-
	lib/relations/-
	languages/Java-1.5/classes/names/classes
	languages/Java-1.5/types/types/subtype
	languages/Java-1.5/types/types/accessibility

rules
	
	create-type-task(|ctx, uri*):
		i@Invoke(Method(m), arg*) -> <type-is(|ctx, [sig])> return-ty
		with
			ctx-uri   := <lookup-uri(|uri*)> NablNsMethod()
		; arg-tys   := <type-task(|ctx)> arg*
		; Use(defs) := <nabl-collect-use> m
		; sigs := <create-disambiguate-call(|ctx, arg-tys, ctx-uri)> defs
		; <task-create-error-on-failure(|ctx, sigs, [arg-tys], ["Cannot invoke method ", m, " with parameter types ", arg-tys, "."])> i
		; ambig := <task-create-error-on-multiple(|ctx, sigs, [arg-tys], ["Method invocation is ambiguous."])> i
		; sig := <task-create-singleton(|ctx)> sigs
		; return-ty := <create-signature-return(|ctx)> sig
		  
	create-type-task(|ctx):
		MethodArgs(a*) -> <type-is(|ctx)> [ty*]
		with
			ty* := <map(type-task(|ctx))> a*

signature

	constructors
		
		DisambiguateCallCreation : List(Def) * List(Type) * URI -> Instruction
		DisambiguateCall : List(Sig) -> Instruction
		
		Signature : Def * List(Type) * Type * List(Modifier) * Throws -> Signature
		GetSignatureReturn : Sig -> Instruction

rules // Disambiguation
	
	create-disambiguate-call(|ctx, a-ty*, ctx-uri):
		def* -> <new-task(|ctx)> DisambiguateCallCreation(def*, a-ty*, ctx-uri)
	
	
	// Task needs to be a combinator because a list of definitions, produced by a task, need to be iterated over.
	task-is-combinator = ?DisambiguateCallCreation(_, _, _)
	task-is-higherorder = ?DisambiguateCallCreation(_, _, _)
	
	perform-task(|task-id):
		DisambiguateCallCreation(def*, a-ty*, ctx-uri) -> DisambiguateCall(sig-d*)
		where
		  a-ty'* := <Hd> a-ty*
		; sig1*  := <map(disambiguate-create-tuple)> def*
		; sig2*  := <filter(where(disambiguate-arity(|<length> a-ty'*)))> sig1*
		; sig3*  := <filter(where(disambiguate-accessible(|task-id, ctx-uri)))> sig2*
		; sig-d* := <map(disambiguate-distances(|task-id, a-ty'*))> sig3*
			
	disambiguate-create-tuple:
		d -> Signature(d, p-ty*, r-ty, mod*, throws)
		where
		  p-ty*  := <get-parameter-types; insert-results-or-delay> d // TODO: don't like this insert-results here..
		; r-ty   := <get-type> d
		; mod*   := <get-modifiers> d
		; throws := <get-throws> d
		  
  disambiguate-arity(|arity):
  	Signature(_, p-ty*, _, _, _) -> <eq> (<length> p-ty*, arity)
  	
	disambiguate-accessible(|ctx, ctx-uri):
		Signature(d, _, _, mod*, _) -> <is-member-accessible(|ctx, mod*, ctx-uri)> d
  		
	disambiguate-distances(|task-id, a-ty*):
		s@Signature(_, p-ty*, _, _, _) -> (s, total-distance)
		where
			total-distance := <zip(disambiguate-distance(|task-id)); task-create-sum(|task-id)> (a-ty*, p-ty*)
		  
  disambiguate-distance(|task-id):
  	(a-ty, p-ty) -> distance
  	with
  	  rel*     := <create-widening-lookup(|task-id)> a-ty
		; distance := <relation-create-distance(|task-id, Widening(), rel*)> p-ty

	
	// Task needs to be a combinator because a list of elements, produced by a task, needs to be sorted.		
	task-is-combinator = ?DisambiguateCall(_)
	
	perform-task(|task-id):
		DisambiguateCall(sig-d*) -> sig*
		where
		  sig-d-sorted* := <sort-list(LSort(disambiguate-compare-distance))> sig-d*
		; sig* := <take-while(?(_, x)); map(Fst)> sig-d-sorted* // Take all signatures with the same distance.
	
	disambiguate-compare-distance:
		((_, [d1]), (_, [d2])) -> <gt> (d1, d2)
		// Distances are in singleton lists because they are task results, and this task is a combinator.


	create-signature-return(|ctx):
		sig -> <new-task(|ctx)> GetSignatureReturn(sig)
		
	perform-task(|task-id):
		GetSignatureReturn(Signature(_, _, ret, _, _)) -> ret
