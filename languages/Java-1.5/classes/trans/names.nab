module languages/Java-1.5/classes/trans/names

imports

  include/Java
  lib/nbl/-
  types

namespaces

  Enum EnumConst Type Constructor Field Method Variable
  
properties

  Throws of Method: Throws
  ParameterTypes of Method: List(Type)

binding rules // Enums

  EnumDec(EnumDecHead(_, Id(e), _), _):
    defines Enum e        // TODO: Should this be in the Type namespace?
    scopes EnumConst
  
  EnumConst(Id(e), _, _):
    defines EnumConst e   // TODO: Should this be in the Field namespace?

binding rules // Types

  ClassDec(ClassDecHead(_, Id(c), _, _, _), _):
    defines Type c 
      of type Type(c)
    scopes Type, Field, Method
    
  InterfaceDec(InterfaceDecHead(_, Id(c), _, _), _):
    defines Type c 
      of type Type(c)
    scopes Type, Field, Method
    
  SuperDec(ClassType(TypeName(Id(c)), _)):
    imports
      Field, imported Field, 
      Method, imported Method
      from Type c

  ImplementsDec(... InterfaceType(TypeName(Id(i)), None()) ...):
    imports
      Field, imported Field, 
      Method, imported Method
      from Type i
      
  InterfaceType(TypeName(Id(i)), None()):
    refers to Type i

binding rules // Constructors

  ConstrDec(ConstrDecHead(_, _, Id(c), p*, th), _):
    defines Constructor c
      of ParameterTypes pt*
      of Throws th
      where p* has type pt*
    scopes Variable
    // TODO: implicitly define this, super

binding rules // Fields

  FieldDec(_, t, ... VarDec(Id(f)) ...):
    defines Field f 
      of type t
    
  FieldDec(_, t, ... VarDec(Id(f), _) ...):
    defines Field f 
      of type t
    
binding rules // Methods

  MethodDec(MethodDecHead(_, _, t, Id(m), p*, th), _):
    defines Method m
      of type t
      of ParameterTypes pt*
      of Throws th
      where p* has type pt*
    scopes Variable
    
  MethodDec(DeprMethodDecHead(_, _, t, Id(m), p*, _, th), _):
    defines Method m
      of type t
      of ParameterTypes pt*
      of Throws th
      where p* has type pt*
    scopes Variable
    
  Param(_, t, Id(p)):
    defines Variable p
      of type t

  VarArityParam(_, t, Id(p)):
    defines Variable p
      of type t
