module languages/Java-1.5/classes/trans/names

imports

  include/Java
  lib/nbl/-
  types
  names
  desugar

namespaces

  Constructor Field Method Variable Label
  
properties

  ParameterTypes of Method      : List(Type)
  Throws         of Method      : Throws
  Modifiers      of Type        : List(ClassMod)
  Modifiers      of Constructor : List(ConstrMod)
  Modifiers      of Method      : List(MethodMod)
  Modifiers      of Field       : List(FieldMod)
  Modifiers      of Variable    : List(VarMod)

binding rules // Enums

  todo -- Should this be in a separate namespace? --
  EnumDec(EnumDecHead(m, Id(e), _), _):
    defines Type e 
      of Modifiers m
    scopes Field
  
  todo -- Should this be in a separate namespace? --
  EnumConst(Id(e), _, _):
    defines Field e   

binding rules // Types

  todo -- implicitly define: this, super. --
  ClassDec(ClassDecHead(m, Id(c), _, _, _), _):
    defines Type c 
      of type Type(c)
      of Modifiers m
    scopes Type, TypeParam, Constructor, Field, Method
    
  todo -- import types? --
  SuperDec(ClassType(c, _)):
    imports
      Field, imported Field, 
      Method, imported Method
      from Type c
  
  todo -- import types? --    
  ImplementsDec([..., InterfaceType(i, _), ...]):
    imports
      Field, imported Field, 
      Method, imported Method
      from Type i
      
binding rules // Constructors

  todo -- implicitly define: this, super. These have a different meaning than the class ones. --
  ConstrDec(ConstrDecHead(m, _, Id(c), p*, th), _):
    defines Constructor c
      of ParameterTypes pt*
      of Throws th
      of Modifiers m
      where p* has type pt*
    scopes Variable

binding rules // Fields

  FieldDec(m, t, [..., VarDec(Id(f)), ...]):
    defines Field f 
      of type t
      of Modifiers m
      in subsequent scope
    
  FieldDec(m, t, [..., VarDec(Id(f), _), ...]):
    defines Field f 
      of type t
      of Modifiers m
      in subsequent scope 
      
binding rules // Methods

  MethodDec(MethodDecHead(mod, _, t, Id(m), p*, th), body):
    defines Method m
      of type t
      of ParameterTypes pt*
      of Throws th
      of Modifiers mod
      where p* has type pt*
    imports Field into body
    scopes Variable, Label
    
  MethodDec(DeprMethodDecHead(mod, _, t, Id(m), p*, _, th), body):
    defines Method m
      of type t
      of ParameterTypes pt*
      of Throws th
      of Modifiers mod
      where p* has type pt*
    imports Field into body
    scopes Variable, Label
    
  Param(m, t, Id(p)):
    defines Variable p
      of type t
      of Modifiers m

  VarArityParam(m, t, Id(p)):
    defines Variable p
      of type t
      of Modifiers m
