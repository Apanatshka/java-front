module languages/Java-1.5/classes/trans/names

imports

	languages/Java-1.5/types/trans/names
  include/Java
  types
  desugar

namespaces

  Constructor
  Field
  Method 
  Variable
  Label
  
properties

  parameter-types of Method      : List(Type)
  throws          of Method      : Throws
  modifiers       of Type        : List(ClassMod)
  modifiers       of Constructor : List(ConstrMod)
  modifiers       of Method      : List(MethodMod)
  modifiers       of Field       : List(FieldMod)
  modifiers       of Variable    : List(VarMod)
  type-parameters of Type        : TypeParams
  type-parameters of Method      : TypeParams

binding rules // Enums

  EnumDec(EnumDecHead(m, Id(e), _), _):
    defines Type e 
      of modifiers m
    scopes Field
  
  EnumConst(Id(e), _, _):
    defines Field e   

binding rules // Types 

  // TODO: implicitly define: this, super
  ClassDec(ClassDecHead(m, Id(c), tp*, None(), _), _):
    defines Type c 
      of type Type(c)
      of modifiers m
      of type-parameters tp*
    implicitly defines Field "this" of type Type(c)
    scopes Type, TypeParam, Constructor, Field, Method
    
  ClassDec(ClassDecHead(m, Id(c), tp*, SuperDec(ClassType(p, tpr*)), _), _):
    defines Type c 
      of type Type(c)
      of modifiers m
      of type-parameters tp*
    imports
      Field, imported Field, 
      Method, imported Method
      from Type p
    	of conformant type-parameters tpr*
    implicitly defines Field "this" of type Type(c)
    implicitly defines Field "super" of type Type(p)
    scopes Type, TypeParam, Constructor, Field, Method
    
  // TODO: desugar?
  // ImplementsDec([..., InterfaceType(i, _), ...]):
  ImplementsDec([InterfaceType(i, _)]):
    imports
      Field, imported Field, 
      Method, imported Method
      from Type i
      
binding rules // Constructors

  // TODO: implicitly define: this, super. These have a different meaning than the class ones.
  ConstrDec(ConstrDecHead(m, _, Id(c), p*, th), _):
    defines Constructor c
      of parameter-types pt*
      of throws th
      of modifiers m
      where p* has type pt*
    scopes Variable

binding rules // Fields

	// TODO: desugar?
  //FieldDec(m, t, [..., VarDec(Id(f)), ...]):
  FieldDec(m, t, [VarDec(Id(f))]):
    defines Field f 
      of type t
      of modifiers m
      in subsequent scope
  
  // TODO: desugar?  
  //FieldDec(m, t, [..., VarDec(Id(f), _), ...]):
  FieldDec(m, t, [VarDec(Id(f), _)]):
    defines Field f 
      of type t
      of modifiers m
      in subsequent scope 
      
binding rules // Methods

	// TODO: imports fields into body
  MethodDec(MethodDecHead(mod, tp*, t, Id(m), p*, th), body):
    defines Method m
      of type t
      of parameter-types pt*
      of throws th
      of modifiers mod
      of type-parameters tp*
      where p* has type pt*
    //imports Field from enclosing Type into body
    scopes Variable, Label
  
  // TODO: imports fields into body  
  MethodDec(DeprMethodDecHead(mod, tp*, t, Id(m), p*, _, th), body):
    defines Method m
      of type t
      of parameter-types pt*
      of throws th
      of modifiers mod
      of type-parameters tp*
      where p* has type pt*
    //imports Field from enclosing Type into body
    scopes Variable, Label
    
  Param(m, t, Id(p)):
    defines Variable p
      of type t
      of modifiers m

  VarArityParam(m, t, Id(p)):
    defines Variable p
      of type t
      of modifiers m
