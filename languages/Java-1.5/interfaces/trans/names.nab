module languages/Java-1.5/interfaces/trans/names

imports

  include/Java
  lib/nbl/-
  types
  names
  desugar

namespaces

  

properties

  Modifiers of Type   : List(InterfaceMod)
  Modifiers of Field  : List(ConstantMod)
  Modifiers of Method : List(AbstractMethodMod)

binding rules // Annotations
  
  AnnoDec(AnnoDecHead(m, Id(a)), _):
    defines Type a
      of type Type(a)
      of Modifiers m
    scopes Type, Field, Method
  
  AnnoMethodDec(mod, t, Id(m), _):
    defines Method m
      of type t
      of Modifiers mod
    scopes Variable
  
  Anno(t, f, _):
  	refers to Field f in Type t
  
binding rules // Interfaces

  InterfaceDec(InterfaceDecHead(m, Id(i), _, _), _):
    defines Type i
      of type Type(i)
      of Modifiers m
    scopes Type, TypeParam, Field, Method
    
  ExtendsDec(i, _):
    imports
      Field, imported Field, 
      Method, imported Method
      from Type i
      // TODO: Import types?  

binding rules // Constants

  ConstantDec(m, t, Id(c), _):
    defines Field c
      of type t
      of Modifiers m
      in subsequent scope
      
binding rules // Asbtract methods

  AbstractMethodDec(mod, _, t, Id(m), p*, th):
  	defines Method m
      of type t
      of ParameterTypes pt*
      of Throws th
      of Modifiers mod
      where p* has type pt*
    scopes Variable

  DeprAbstractMethodDec(mod, _, t, Id(m), p*, _, th):
    defines Method m
      of type t
      of ParameterTypes pt*
      of Throws th
      of Modifiers mod
      where p* has type pt*
    scopes Variable
