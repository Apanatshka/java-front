module languages/Java-1.5/statements/trans/names

imports

  include/Java
  languages/Java-1.5/names/trans/names

namespaces

  

binding rules // Local variables

	// TODO: desugar?
  // LocalVarDec(m, t, [..., VarDec(Id(v)), ...]):
  LocalVarDec(m, t, [VarDec(Id(v))]):
    defines Variable v
      of type t
      of Modifiers m
      in subsequent scope
  
  // TODO: desugar?    
  // LocalVarDec(m, t, [..., VarDec(Id(v), _), ...]):
  LocalVarDec(m, t, [VarDec(Id(v), _)]):
    defines Variable v 
      of type t
      of Modifiers m
      in subsequent scope

binding rules // Labels

  Labeled(Id(l), _):
  	defines Label l
  	
  Continue(Some(Id(l))):
  	refers to Label l

  Break(Some(Id(l))):
    refers to Label l
    
binding rules // Control flow

  If(_, _):
    scopes Variable
    
  // TODO: incorrect scoping, if and else parts need their own scope.
  If(_, _, _):
    scopes Variable
  	
  While(_, _):
  	scopes Variable
  	
  DoWhile(_, _):
  	scopes Variable
  
  // TODO: desugar?
  // TODO: Refactor (syntax) so that this does not require 4 different binding rules?
  // TODO: The nested VarDecs already define variables..
  // For(LocalVarDec(m, t, [..., VarDec(Id(v)), ...]), cond, inc, stm):
  For(LocalVarDec(m, t, [VarDec(Id(v))]), cond, inc, stm):
    defines Variable v 
      of type t
      of Modifiers m
      in cond, inc, stm
  	scopes Variable
  
  // TODO: desugar?
  // TODO: The nested VarDecs already define variables..	
  // For(LocalVarDec(m, t, [..., VarDec(Id(v, _)), ...]), cond, inc, stm):
  For(LocalVarDec(m, t, [VarDec(Id(v), _)]), cond, inc, stm):
    defines Variable v 
      of type t
      of Modifiers m
      in cond, inc, stm
  	scopes Variable
  
  // TODO: The nested Param already defines a variable..	
  For(Param(m, t, v), cond, inc, stm):
    defines Variable v 
      of type t
      of Modifiers m
      in cond, inc, stm
  	scopes Variable
  
  // TODO: The nested VarArityParam already defines a variable..	
  For(VarArityParam(m, t, v), cond, inc, stm):
    defines Variable v 
      of type t
      of Modifiers m
      in cond, inc, stm
  	scopes Variable
  	
	// TODO: Refactor (syntax) so that this does not require 2 different binding rules?
	// TODO: The nested Param already defines a variable..
  ForEach(Param(m, t, v), list, stm):
  	defines Variable v
  	  of type t
  	  of Modifiers m
  	  in stm
  	  
  // TODO: The nested VarArityParam already defines a variable..
  ForEach(VarArityParam(m, t, v), list, stm):
  	defines Variable v
  	  of type t
  	  of Modifiers m
  	  in stm
  
  // TODO: Refactor (syntax) so that this does not require 2 different binding rules?
  // TODO: The nested Param already defines a variable..
  Catch(Param(m, t, v), stm):
  	defines Variable v
  	  of type t
  	  of Modifiers m
  	  in stm
  
  // TODO: The nested VarArityParam already defines a variable..	  
  Catch(VarArityParam(m, t, v), stm):
  	defines Variable v
  	  of type t
  	  of Modifiers m
  	  in stm

  SwitchGroup(_, _):
    scopes Variable
    
  Block(_):
    scopes Variable
